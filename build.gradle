task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.android.tools.build', name: 'gradle', version: '1.2.3'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/twofortyfouram/maven' }
    }
}

apply plugin: 'com.android.application'

dependencies {
    compile group: 'com.android.support', name: 'design', version: '22.2.0'

    compile group: 'com.twofortyfouram', name: 'android-plugin-api-for-locale', version: '[1.0.1,2.0['

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.1.1-2'
    apk group: 'com.github.tony19', name: 'logback-android-core', version: '1.1.1-2'
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionCode 2
        versionName "0.2.0"
        minSdkVersion 8
        targetSdkVersion 22
    }

    signingConfigs {
        release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.txt'
            signingConfig signingConfigs.release
        }
    }

    if (project.hasProperty('keyAlias') &&
            project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyPassword')) {
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        buildTypes.release.signingConfig = null
    }
}
